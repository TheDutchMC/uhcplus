import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '5.0.0'
	id 'eclipse'
}

allprojects {

	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	version = pluginVersion
	group = pluginGroup	

	repositories {
		jcenter()
		mavenCentral()
		mavenLocal()
		
		maven {
			url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
		}
		
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots"
		}
		
		maven {
			url "https://maven.enginehub.org/repo/"
		}
		
		maven {
	    	name 'papermc'
	    	url 'https://papermc.io/repo/repository/maven-public/'
		}
	}
	
	processResources {
		from(sourceSets.main.resources.srcDirs) {
			filter ReplaceTokens, tokens: [version: version]
		}
	}
}

dependencies {

	//Spigot API
	compileOnly 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
	
	//Apache Commons
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	
	//WorldEdit API
	compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '7.1.0'
	
	//PaperLib
    compile "io.papermc:paperlib:1.0.4"
}

task testJar(type: ShadowJar) {

	doFirst {
		relocate 'io.papermc.lib', 'nl.thedutchmc.libs.paperlib'
		relocate 'org.apache.commons.io', 'nl.thedutchmc.libs.commons.io'
	}

	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	destinationDirectory = file("$rootDir/server/plugins")
	configurations = [project.configurations.runtime]
	from sourceSets.main.output
}

task releaseJar(type: ShadowJar) {

	doFirst {
		relocate 'io.papermc.lib', 'nl.thedutchmc.libs.paperlib'
		relocate 'org.apache.commons.io', 'nl.thedutchmc.libs.commons.io'
	}

	description 'Build a release jar'
	archiveClassifier = 'RELEASE'
	destinationDirectory = file("$rootDir/releases")
	configurations = [project.configurations.runtime]
	from sourceSets.main.output
}
